#### 2.1

```
PREFIX dbo: <http://dbpedia.org/ontology/>
PREFIX dbp: <http://dbpedia.org/property/>
PREFIX res: <http://dbpedia.org/resource/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

SELECT DISTINCT ?uri ?string 
WHERE {
        res:Bill_Clinton dbo:child ?child .
        ?child dbp:spouse ?uri .
        ?uri rdfs:label ?string .
		FILTER(lang(?string)='en') .
}
```



```
PREFIX dbo: <http://dbpedia.org/ontology/>
PREFIX res: <http://dbpedia.org/resource/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
SELECT DISTINCT ?uri ?string 
WHERE {       
        res:Brooklyn_Bridge dbo:crosses ?uri . 
        OPTIONAL { ?uri rdfs:label ?string. FILTER (lang(?string) = 'en') }
}
```



```
PREFIX dbo: <http://dbpedia.org/ontology/>
PREFIX dbp: <http://dbpedia.org/property/>
PREFIX res: <http://dbpedia.org/resource/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

SELECT DISTINCT ?uri ?string WHERE
{ 
        res:Abraham_Lincoln dbp:deathPlace ?uri .
        OPTIONAL { ?uri rdfs:label ?string. FILTER (lang(?string) = 'en') }
}
```



#### 2.2

```
PREFIX dbo: <http://dbpedia.org/ontology/>
PREFIX dbp: <http://dbpedia.org/property/>
PREFIX res: <http://dbpedia.org/resource/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

# Quelle taille fait Claudia Schiffer

SELECT ?number WHERE {
	res:Claudia_Schiffer dbo:height ?number .
}
```





#### 2.3

```
PREFIX dbo: <http://dbpedia.org/ontology/>
PREFIX dbp: <http://dbpedia.org/property/>
PREFIX res: <http://dbpedia.org/resource/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

# Quels sont les  ́etats frontaliers de l’Illinois ?
# il existe une property de Illinois qui donne une r ́eponse tr`es rapide

SELECT DISTINCT ?states ?stateName WHERE {
	res:Illinois dbp:borderingstates ?states .
	OPTIONAL {
		?states rdfs:label ?stateName .
		FILTER(lang(?stateName)='en')
		}
}
```

 



#### 2.4

```
PREFIX dbo: <http://dbpedia.org/ontology/>
PREFIX dbp: <http://dbpedia.org/property/>
PREFIX res: <http://dbpedia.org/resource/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX dbc: <http://dbpedia.org/resource/Category:>

# Quelles sont les capitales de tous les pays d’Afrique ?
# 	Plusieurs relations permettent de dire qu’un pays est en Afirque, 
# 	soit en utilisant le lien dbo:wikiPageWikiLink 
# 	soit en utilisation la ressource dbc:Countries_in_Africa

SELECT DISTINCT ?capital ?cityName WHERE {
	?country dbo:wikiPageWikiLink dbc:Countries_in_Africa.
	?country dbo:capital ?capital .
	OPTIONAL {
		?capital rdfs:label ?cityName .
		FILTER(lang(?cityName)='en')
		}
}
```



#### 2.5

```
PREFIX dbo: <http://dbpedia.org/ontology/>
PREFIX dbp: <http://dbpedia.org/property/>
PREFIX res: <http://dbpedia.org/resource/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

# In which country does the Nile originate?
# 	More ways to express the source (eg exdbp:source1Location or dbp:source or ...), 
#	and the same property is sometimes used for linking the Nile to a country,
#	or sometimes the Nile to a country label...
#	Attention, dbp:source sometimes refers to a region whose countries are subdivisions...
#	Attention, some countries do not have a label, put it OPTIONAL

SELECT DISTINCT ?country ?countryName WHERE {
	res:Nile dbp:source2Location ?country .
	?country dbo:countryCode ?code. 
	OPTIONAL {
		?country rdfs:label ?countryName .
		FILTER(lang(?countryName)='en')
	}
}
```



#### 2.6

```
PREFIX dbo: <http://dbpedia.org/ontology/>
PREFIX dbp: <http://dbpedia.org/property/>
PREFIX res: <http://dbpedia.org/resource/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

# Which cities in France have more than 100,000 inhabitants?
# 	several ways to express that a city is in France (by the page template
#	dbp:wikiPageUsesTemplate or by the rel dbo:country) 
#	and to represent France (dbr:France or yago:France)

SELECT DISTINCT  ?cities ?cityName ?population WHERE {
	?cities dbo:country res:France.
	?cities dbp:urbanPop ?population 
	FILTER(?population >= 100000)
	OPTIONAL {
		?cities rdfs:label ?cityName .
		FILTER(lang(?cityName)='en')
	}
}
```

